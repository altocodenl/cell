- "TEXT <> PATHS" - ct ""
                    r ""
                    tag "Empty text"

                  - ct "foo"
                    tag "Single text"

                  - ct "1"
                    tag "Single number"

                  - ct "1"
                    tag "Single number"

                  - ct " "
                    r ""
                    tag "Line with a space"

                  - ct " s"
                    r error "The first line of the message cannot be indented: ` s`."
                    tag "Indented first line"

                  - ct "a	b"
                    r error "The line `a	b` contains a space that should be contained within quotes."
                    tag "Unquoted tab"

                  - ct "holding/"out"
                    r error "The line `holding/"out` has an unescaped quote."
                    tag "Unescaped literal quote"

                  - ct "/"multiline
                         trickery/" some 2 /"calm
                                           animal/""
                    tag "Multiline texts in a long path"

                  - ct "a b
                        c d"
                    tag "Two paths of two steps"

                  - ct "foo bar 1
                           jip 2"
                    r error "The indent of the line `   jip 2` does not match that of the previous line."
                    tag "Indent too short"

                  - ct "widget count 1
                                                jip 2"
                    r error "The indent of the line `                        jip 2` does not match that of the previous line."
                    tag "Indent too long"

                  - ct "widget count 1
                               jip  2"
                    r error "The line `       jip  2` has at least two spaces separating two elements."
                    tag "Too many spaces between paths"

                  - ct "widget count 1
                               price 12"
                    tag "Two paths with one prefix"

                  - ct "widget count 1
                        widget price 12"
                    r "widget count 1
                              price 12"
                    tag "Two unabridged paths with one prefix come back abridged"

                  - ct "deep widget count 1
                                    price 12"
                    tag "Two paths with one prefix two steps long"

                  - ct "deep widget count 1
                        deep widget price 12"

                    r "deep widget count 1
                                   price 12"
                    tag "Two paths with one prefix two steps long come back abridged"

                  - ct "deep widget count 1
                             widget price 12"

                    r "deep widget count 1
                                   price 12"
                    tag "Two paths with one prefix two steps long (semiabridged) come back abridged"

                  - ct "1 foo bar
                        1 jip heh
                        1 sub acu"
                    r "1 foo bar
                         jip heh
                         sub acu"
                    tag "List comes back abridged"

                  - ct "product price 0.1
                                quantity 2.75"
                    tag "Two paths with float values"

                  - ct "product 1 count
                                2 price"
                    tag "List"

                  - ct "something/"foo/" 1"
                    r error "The line `something/"foo/" 1` has an unescaped quote."
                    tag "Unescaped quote in word"

                  - ct "/"foo bar/" 1"
                    tag "Quoted text as path"

                  - ct "/"foo/" /"bar/" 1"
                    r "foo bar 1"
                    tag "Quoted texts that do not need to be quoted come back unquoted"

                  - ct "/"///"foo///" ///"bar///" 1/""
                    tag "Quoted text with quotes inside"

                  - ct "/"foo bar/"x1"
                    r error "No space after a quote in line `/"foo bar/"x1`"
                    tag "No space after quote"

                  - ct "foo /"bar
                             i am on a new line but I am still the same text/" 1"
                    tag "Quote spanning multiple lines"

                  - ct "foo /"1/" bar"
                    tag "Path with text that looks like a number"

                  - ct "foo /"	/" bar"
                    tag "Path with tab"

                  - ct "/"i am text

                         right?
                         /""
                    tag "Quoted text with empty line"

                  - ct "foo /"bar/""
                    r "foo bar"
                    tag "Unnecessary quotes on one step"

                  - ct "foo /"bar yep/""
                    tag "Quoted step in a path"

                  - ct "date 2025-01-01"
                    tag "A date can be a step"

                  - ct "empty /"/" indeed"
                    tag "An empty text can be a step of a path"

                  - ct "/"another multiline

                         /"foo"
                    r error "No space after a quote in line ` /"foo`"
                    tag "No space after multiline text"

                  - ct "/"just multiline
                         ////
                         ///"/""
                    tag "Quoted text with slashes and literal quotes inside"

                  - ct "/"///"/""
                    tag "Single literal quote as step"

                  - ct "/" /////""
                    tag "Space and a slash"

                  - ct "/" /////////""
                    tag "Space and two slashes"

                  - ct "////"
                    tag "Two slashes"

                  - ct "/"/////""
                    r "//"
                    tag "Two slashes unnecessarily quoted"

                  - ct "/"///////"/""
                    tag "Slash and a quote"

                  - ct "/" /////" /" /////////" //// /" ////a/""
                    tag "Space and slash, space and two slashes, space and three slashes, space and four slashes, space, slash and `a`."

                  - ct "history /"1/" from user
                                    message /"@ foo/""
                    tag "Quoted number and quoted text as steps."

                  - ct "history /" /////" from user
                                      message /"@ foo/""
                    tag "Step with multiple slashes"

                  - ct "/" ///""
                    r error "Multiline text not closed: ` /"`"
                    tag "Unclosed multiline"

                  - ct "/" //a/""
                    r error "Unmatched slash in text with spaces or double quotes: ` //a`"
                    tag "Unmatched slash"

                  - ct "/"
                         //a"
                    r error "Unmatched slash in text with spaces or double quotes: `//a`"
                    tag "Unmatched slash in multiline text"

                  - ct "/" //////a"
                    r error "Unmatched slash in text with spaces or double quotes: ` //////a`"
                    tag "Unmatched slash with multiple slashes."

                  - ct "/"A single call must start with ///"@///" but instead starts with ///"w///"/""
                    tag "Long quoted text"

                  - ct "foo - first
                            - second
                            - third"
                    r "foo 1 first
                           2 second
                           3 third"
                    tag "Dedasher with list with three elements"

                  - ct "bar - first
                        foo - second"
                    r "bar 1 first
                       foo 1 second"
                    tag "Dedasher with two consecutive lists"

                  - ct "foo jip 1
                            bar 2"
                    r "foo bar 2
                           jip 1"
                    tag "Sort lowercase hashes"

                  - ct "foo bar 1
                        Foo jip 2"
                    r "Foo jip 2
                       foo bar 1"
                    tag "Uppercase goes after lowercase"

                  - ct "Foo bar 1
                        foo jip 2"
                    r "Foo bar 1
                       foo jip 2"
                    tag "Uppercase goes before lowercase, no-op"

                  - ct "foo bar 1
                            bar 2"
                    r error "The path `foo bar 2` is repeated."
                    tag "Repeated path"

                  - ct "foo bar 3
                            bar"
                    r error "The path `foo bar 3` is setting a hash but there is already a text at path `foo`"
                    tag "Two paths with the same prefix: the shortest one should be the only one considered valid"

                  - ct "foo bar 3 hash 1
                            bar"
                    r error "The path `foo bar 3 hash 1` is setting a hash but there is already a text at path `foo`"
                    tag "Two paths with the same prefix: the shortest one should be the only one considered valid (long one having three steps more)"

                  - ct ": foo
                        = bar"
                    r "= bar
                       : foo"
                    tag "Standalone colon goes after standalone equal"

                  - ct "=bar 1
                        :foo 2"
                    r ":foo 2
                       =bar 1"
                    tag "Text starting with colon goes before text starting with equal"

                  - ct "foo
                        bar"
                    r error "The path `foo` is repeated."
                    tag "Values without prefixes"

                  - ct "foo 1
                        foo 2"
                    r error "The path `foo 2` is repeated."
                    tag "Repeated hash elements"

                  - ct "foo 1
                        bar 2
                        foo 3"
                    r error "The path `foo 3` is repeated."
                    tag "Repeated non-consecutive hash elements"

                  - ct "foo jip
                            bar 1"
                    r error "The path `foo jip` is setting a text but there is already a hash at path `foo`"
                    tag "Type clash: text on hash"

                  - ct "foo mau5 bar
                                 jip 1"
                    r error "The path `foo mau5 jip 1` is setting a hash but there is already a text at path `foo mau5`"
                    tag "Type clash: hash on text"

                  - ct "foo jip 1
                            2 2"
                    r error "The path `foo jip 1` is setting a hash but there is already a list at path `foo`"
                    tag "Type clash: hash on list"

                  - ct "foo
                        1"
                    r error "The path `foo` is setting a text but there is already a number at path ``"
                    tag "Type clash: text on number"

                  - ct "- yes"
                    r "1 yes"
                    tag "Dashed list with one element"

                  - ct "foo first 1
                        bar x
                        foo second 2"
                    r "bar x
                       foo first 1
                           second 2"
                    tag "Unabridged, unsorted split prefix"

- "JS <> PATHS" - js "{/"c/": /"d/", /"a/": /"b/"}"
                  r "a b
                     c d"
                  tag "Parse JSON object"

                - js "[1, 2, 3]"
                  r "1 1
                     2 2
                     3 3"
                  tag "Parse JSON list"

                - js "/"json/""
                  r "json"
                  tag "Parse JSON text"

                - js "1"
                  r "1"
                  tag "Parse JSON number"

                - js "true"
                  r "1"
                  tag "Parse JSON boolean"

                - js "null"
                  r "/"/""
                  tag "Parse JSON null"

                - js "{/"boo/": [true, false], /"null/": null, /"why/": /"2025-11-12T20:53:20.774Z/"}"
                  r "boo 1 1
                         2 0
                     null /"/"
                     why 2025-11-12T20:53:20.774Z"
                  tag "Parse object with booleans, null and date"

                - cjs ""
                  r "/"/""
                  tag "Empty text outputs empty text"

                - cjs "/"/""
                  r "/"/""
                  tag "An empty text also outputs empty text"

                - cjs "1"
                  r "1"
                  tag "Number to JSON"

                - cjs "/"1/""
                  r "/"1/""
                  tag "Quoted number to JSON"

                - cjs "2 second"
                  r "[null, /"second/"]"
                  tag "Sparse list"

                - cjs "jup yea
                       soda wey
                       bar 1 jip
                           2 joo"
                  r "{/"bar/": [/"jip/", /"joo/"], /"jup/": /"yea/", /"soda/": /"wey/"}"
                  tag "Four paths into JSON"

- "GET & PUT" - c @
                r ""
                tag "Get everything when the dataspace is empty"

              - c @ x
                r ""
                tag "Get one prefix when the dataspace is empty"

              - c @ put party list 1 Jan
                                   2 Jip
                              open 1
                r diff + party list 1 Jan
                                    2 Jip
                               open 1
                tag "Create a hash with three paths"

              - c @ put serious business
                r diff + serious business
                tag "Create another hash"

              - c @
                r party list 1 Jan
                             2 Jip
                        open 1
                  serious business
                tag "Get everything and obtain both hashes"

              - c @ party
                r list 1 Jan
                       2 Jip
                  open 1
                tag "Get one prefix to get the entire party"

              - c @ party list
                r 1 Jan
                  2 Jip
                tag "Get a longer prefix to get just the list"

              - c @ party list 1
                r Jan
                tag "Get the first guest only"

              - c @ party list 1 Jan
                r ""
                tag "Get the entire first path and obtain nothing"

              - c @ no such thing
                r ""
                tag "Get no such thing"

              - c @ party open
                r 1
                tag "Get the status of the party"

              - c @ serious
                r business
                tag "Get just the business"

              - c @ serious business
                r ""
                tag "Get the entire last path and obtain nothing"

              - c @ put party 1
                r diff + party 1
                       x party list 1 Jan
                                    2 Jip
                               open 1
                tag "Overwrite prefix with multiple paths"

              - c @
                r party 1
                  serious business
                tag "Get everything after overwriting prefix with multiple paths"

              - c @ put party list 1 Jan
                                   2 Jip
                              open 1
                r diff + party list 1 Jan
                                    2 Jip
                               open 1
                       x party 1
                tag "Restore party"

- WIPE - c @ wipe serious
         r diff x serious business
         tag "Wipe serious"

       - c @
         r party list 1 Jan
                      2 Jip
                 open 1
         tag "Get everything after serious was wiped"

       - c @ wipe party list 1 Jan Hertog
         r diff ""
         tag "Wipe a path that does not exist, should change nothing"

       - c @
         r party list 1 Jan
                      2 Jip
                 open 1
         tag "Get everything after wipe no-op"

       - c @ wipe party list 1
         r diff x party list 1 Jan
         tag "Wipe the first member of the list"

       - c @ party
         r list 1 Jip
           open 1
         tag "Get party after one element was wiped"

       - c @ wipe
         r diff x party list 1 Jip
                        open 1
         tag "Wipe party"

       - c @
         r ""
         tag "Get everything after wipe and obtain nothing"

       - c @ put nested 1 list 1 Jan
                               2 Jip
                        2 open
         tag "Setting a nested list"

       - c @ wipe nested 1 list 1
         tag "Wiping part of a nested list"

       - c @
         r nested 1 list 1 Jip
                  2 open
         tag "Get everything after wiping part of a nested list"

       - c @ wipe

       - c @
         r ""
         tag "Wipe everything"

       - c @ put a b
                 c d e
                   f g
                 h i

       - c @ wipe - a
                  - c d
         r diff x a b
                  c d e

       - c @
         r c f g
           h i
         tag "Wipe two prefixes, passing list"

       - c @ wipe

       - c @ put a b 1 x
                   c 2 y
                   d 3 z

       - c @ wipe a b 1
                    d 3

       - c @
         r a c 1 y
         tag "Wipe two prefixes, passing hash"
         comment "Note that the step went from 2 to 1, because wipe recalculates the indexes of lists when it removes paths"

       - c @ wipe

       - c @ put a b
                 c @ a

       - c @
         r a b
           c = b
             @ a

       - c @ wipe a
         r diff x a b

       - c @
         r c = ""
             @ a
         tag "Wipe updates calls"

       - c @ wipe

- "GET WITH CONTEXT" - c @ put employee price 15
                               friend price 20
                               price 30
                       tag "Initialize the three prices"

                     - c @
                       r employee price 15
                         friend price 20
                         price 30
                       tag "Verify that the three prices are initialized"

                     - c @ price
                       r 30
                       tag "Get outermost price"

                     - c @ put employee gets @ price
                       tag "Set reference to employee price"

                     - c @ employee gets
                       r = 15
                         @ price
                       tag "Get employee price"

                     - c @ put friend gets @ price
                       tag "Set reference to friend price"

                     - c @ friend gets
                       r = 20
                         @ price
                       tag "Get friend price"

                     - c @ put outside gets @ price
                       tag "Set reference to outside price"

                     - c @ outside gets
                       r = 30
                         @ price
                       tag "Get outside price"

                     - c @ wipe

                     - c @ put inside list 1 no
                                           2 maybe
                                      ref @ list 3
                               list 3 yes

                     - c @ inside ref
                       r = ""
                         @ list 3
                       tag "Get walks up with a single hook, doesn't find anything"

                     - c @ put inside list 3 ""

                     - c @ inside ref
                       r = ""
                         @ list 3
                       tag "Get walks up with a single hook, finds an empty text and stops there"

                     - c @ put inside list 3 yes

                     - c @ inside ref
                       r = yes
                         @ list 3
                       tag "Get walks up with a single hook, finds something"

                     - c @ wipe

- "GET WITH DOT" - c @ put employee @ . price
                                    price 15
                           @ . price
                           price 30
                           z @ . price
                   tag "Set two prices and three dot references"

                 - c @
                   r employee price 15
                              = 15
                              @ . price
                     price 30
                     z = ""
                       @ . price
                     = 30
                     @ . price

                   tag "Get all dot references"

                 - c @ wipe

                 - c @ wipe
                   comment "We wipe twice in a row to have an empty diff on the next test"

- PUT - c @ put key value
      - c @
        r dialog 1 c "@ wipe"
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r diff ""
                   to cell
                 2 c "@ put key value"
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r diff + key value
                   to cell
          key value
        dialog 1
        tag "Get dataspace including dialog"

      - c @ put something else
      - c @
        r dialog 1 c "@ wipe"
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r diff ""
                   to cell
                 2 c "@ put key value"
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r diff + key value
                   to cell
                 3 c @
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r [OMITTED]
                   to cell
                 4 c "@ put something else"
                   from user
                   hide 1
                   id [OMITTED]
                   ms [OMITTED]
                   r diff + something else
                   to cell
          key value
          something else
        dialog 1
        tag "Get dataspace including dialog after two more calls"

      - c @ wipe

      - c @ put data list - a
                          - b

      - c @ put data just this path

      - c @ data
        r just this path
          list 1 a
               2 b
        tag "Add another path to a partially overlapping prefix"

      - c @ put data simple
        r diff + data simple
               x data just this path
                      list 1 a
                           2 b

      - c @ data
        r simple
        tag "Overwrite long path with a short one"

      - c @ put data hash 1
        r diff + data hash 1
               x data simple

      - c @ put data 2 3
        r diff + data 2 3
               x data hash 1

      - c @ data
        r 2 3
        tag "Replace a hash with a list"

      - c @ put data more 3
        r diff + data more 3
               x data 2 3

      - c @ data
        r more 3
        tag "Replace a list with a hash"

      - c @ wipe

      - c @ put 1
        r error "Cannot set entire dataspace to something that is not a hash"
        tag "Cannot overwrite entire dataspace with number"

      - c @ put foo
        r error "Cannot set entire dataspace to something that is not a hash"
        tag "Cannot overwrite entire dataspace with text"

      - c @ put 1 list
        r error "Cannot set entire dataspace to something that is not a hash"
        tag "Cannot overwrite entire dataspace with list"

      - c @ put foo ""

      - c @
        r foo ""
        tag "Empty text is set and retrieved"

      - c @ put "" bar

      - c @
        r foo ""
          "" bar
        tag "Empty text can be used as path"

      - c @ ""
        r bar
        tag "Get value from path that has an empty text"

      - c @ wipe

      - c @ put hash one thing

      - c @ put hash "
                            bar" yes
      - c @ hash
        r one thing
          "
                 bar" yes
        tag "Retrieve a value with a path with a step that is multiline text"

      - c @ put dialog 1
        r error "A dialog cannot be supressed by force."
        tag "Forbid putting to the dialog"

      - c @ wipe

      - c @ put foo bar jip 1
                oh yeah 1

      - c @
        r foo bar jip 1
          oh yeah 1
        tag "Allow putting multiple paths at the same time"

      - c @ wipe

      - c @ put zero change
        r diff + zero change

      - c @ put zero change
        r diff ""

      - c @ put zero one
        r diff + zero one
               x zero change

      - c @ wipe

      - c @ put a 1 a
                b 2 b
                c 3 c

      - c @
        r a 1 a
          b 2 b
          c 3 c
        tag "Get sparse list"

      - c @ wipe

- "PUT WITH CONTEXT PATH" - c @ put hook something

                          - c @ put value 1
                            context - foo
                            r diff + value 1

                          - c @
                            r hook something
                              value 1
                            tag "Context path does not make a difference if there is no hook"

                          - c @ wipe
                          - c @ put hook value 0
                                    value 1

                          - c @
                            r hook value 0
                              value 1
                            tag "Hook without context is irrelevant"

                          - c @ put value 2
                            context - hook
                            r diff + hook value 2
                                   x hook value 0

                          - c @
                            r hook value 2
                              value 1
                            tag "Hook with context"

                          - c @ wipe

                          - c @ put mothership count 1
                                               deeper context

                          - c @ put count 2
                            context - mothership
                                    - deeper
                                    - context
                            r diff + mothership count 2
                                   x mothership count 1

                          - c @
                            r mothership count 2
                                         deeper context

                          - c @ wipe

                          - c @ put inside list 1 no
                                                2 maybe
                                    list 1 outer


                          - c @ put list 3 yes
                            context - inside
                                    - list
                                    - 1
                            r diff + inside list 3 yes

                          - c @
                            r inside list 1 no
                                          2 maybe
                                          3 yes
                              list 1 outer
                            tag "Put walks up with a single hook, finds something"

                          - c @ put inside list 3 ""

                          - c @ put list 3 yes
                            context - inside
                                    - list
                                    - 1
                            r diff + inside list 3 yes
                                   x inside list 3 ""

                          - c @
                            r inside list 1 no
                                          2 maybe
                                          3 yes
                              list 1 outer
                            tag "Put walks up with a single hook, finds an empty text and stops there"

                          - c @ wipe

- "PUT WITH DOT" - c @ put ball 1
                           pit ball 2

                 - c @ put . ball 3
                   r diff + ball 3
                          x ball 1

                 - c @
                   r ball 3
                     pit ball 2
                   tag "Use dot without context"

                 - c @ put . ball 4
                   context - completely
                           - different
                   r diff + completely different ball 4

                 - c @
                   r ball 3
                     completely different ball 4
                     pit ball 2
                   tag "Use dot with non-existing context, creates the context"

                 - c @ put . ball 5
                   context - pit
                   r diff + pit ball 5
                          x pit ball 2

                 - c @
                   r ball 3
                     completely different ball 4
                     pit ball 5
                   tag "Use dot with existing context"

                 - c @ wipe

- "WIPE WITH CONTEXT PATH" - c @ put hook value 2
                                     value 1

                           - c @ wipe value
                             context - nonesuch
                             r diff x value 1

                           - c @
                             r hook value 2
                             tag "Context path does not make a difference if there is no hook"

                           - c @ wipe value
                             context - hook
                             r diff x hook value 2

                           - c @
                             r ""
                             tag "Context path makes a difference if a hook is found"

                           - c @ wipe

- "WIPE WITH DOT" - c @ put hook value 2
                            value 1

                  - c @ wipe . value
                    context - nonesuch
                    r diff ""

                  - c @
                    r hook value 2
                      value 1
                    tag "Dot with context will not walk up"

                  - c @ wipe . value
                    r diff x value 1

                  - c @
                    r hook value 2
                    tag "Top level dot will remove path"

                  - c @ wipe

- RESPOND - c @ put foo 10
                    reffoo @ foo
            tag "Set a reference to foo"

          - c @
            r foo 10
              reffoo = 10
                     @ foo
            tag "Get the reference to foo"

          - c @ put foo 20
            tag "Update foo"

          - c @
            r foo 20
              reffoo = 20
                     @ foo
            tag "Check that the reference to foo has an updated value"

          - c @ put reffoo = 30
            tag "Overwrite the = key of the reference"

          - c @
            r foo 20
              reffoo = 20
                     @ foo
            tag "Check that overwriting the result made no difference"

          - c @ put a @ nonesuch

          - c @ a
            r = ""
              @ nonesuch
            tag "Check that a reference to nothing yields an empty text but the reference is still there"

          - c @ wipe

          - c @ put counter 1
                    indirect @ users @ counter
                    users 1 jip

          - c @
            r counter 1
              indirect = jip
                       @ users = 1
                               @ counter
              users 1 jip
            tag "Use a reference to reference a certain element of a list (two references side by side)"

          - c @ wipe
          - c @ put aqualung great
                    indirect @ score @ aqualung
                    score great 100

          - c @
            r aqualung great
              indirect = 100
                       @ score = great
                               @ aqualung
              score great 100
            tag "Use a reference to reference a certain element of a hash (two references side by side)"

          - c @ wipe
          - c @ put closet sock
                    house @ room
                    room @ closet

          - c @
            r closet sock
              house = = sock
                      @ closet
                    @ room
              room = sock
                   @ closet
            tag "Make a reference to a reference in order ba//dc//cb"

          - c @ wipe
          - c @ put foo bar
                    joo @ jip

          - c @
            r foo bar
              joo = ""
                  @ jip
            tag "Make a reference to a reference in order ba//cb//dc, step one"

          - c @ put jip @ foo

          - c @
            r foo bar
              jip = bar
                  @ foo
              joo = = bar
                    @ foo
                  @ jip
            tag "Make a reference to a reference in order ba//cb//dc, step two"

          - c @ wipe
          - c @ put location trompe 1
                    pixies trompe 1 "alec eiffel"
                                  2 mons
                    song @ pixies @ location

          - c @ song
            r = "alec eiffel"
              @ pixies = trompe 1
                       @ location
            tag "Two references side by side, the right one resolving to a one-line hash"

          - c @ wipe
          - c @ put list 1 place
                         2 without
                         3 a
                         4 postcard
                    ref @ list

          - c @ ref
            r = 1 place
                2 without
                3 a
                4 postcard
              @ list
            tag "Reference a list"

          - c @ put list REDACTED

          - c @
            r list REDACTED
              ref = REDACTED
                  @ list
            tag "Overwrite referenced list by text"

          - c @ wipe
          - c @ put main stores 2
                         total @ widgets
                    widgets 10

          - c @
            r main stores 2
                   total = 10
                         @ widgets
              widgets 10
            tag "Reference inside a hash"

          - c @ wipe
          - c @ put ned 1 a A C
                        2 b B D
                    quinella 1 a
                             2 b
                    ref @ ned @ quinella

          - c @ ref
            r = A C
              @ ned = 1 a
                      2 b
                    @ quinella
            tag "Getting back two paths as part of a reference, using just the first one"
            note "An alternative way to specify this would be to expect this:

                  = A C
                    B D
                  @ ned = 1 a
                          2 b
                        @ quinella

                  But, for now, we don't need this multireference. It even seems that it's better to just implement multireference as a loop. Time will tell. We go with the above for now.
                  "

          - c @ wipe

- CONDITIONAL - c @ put result @ if cond @ count
                                    do yes!
                                    finally no!
              - c @
                r result = error "An if call has to be a hash with keys `cond`, `do` and `else`."
                         @ if cond = ""
                                   @ count
                              do yes!
                              finally no!
                tag "Extraneous key in if"

              - c @ wipe

              - c @ put result @ if do yes!
                                    else no!

              - c @
                r result = error "An if call has to contain a `cond` key."
                         @ if do yes!
                              else no!
                tag "Missing cond in if"

              - c @ wipe

              - c @ put count 1
                        result @ if cond @ count
                                    do yes!
                                    else no!

              - c @
                r count 1
                  result = yes!
                         @ if cond = 1
                                   @ count
                              do yes!
                              else no!
                tag "If with truthy cond"

              - c @ put count 0

              - c @
                r count 0
                  result = no!
                         @ if cond = 0
                                   @ count
                              do yes!
                              else no!
                tag "If with falsy cond"

              - c @ wipe result @ if
              - c @ put result @ if cond @ count
                                    else no!

              - c @
                r count 0
                  result = no!
                         @ if cond = 0
                                   @ count
                              else no!
                tag "If with falsy cond and no do"

              - c @ put count 1

              - c @
                r count 1
                  result = ""
                         @ if cond = 1
                                   @ count
                              else no!
                tag "If with truthy cond and no do"

              - c @ wipe

              - c @ put count 0
                        inner count 1
                              result @ if cond @ count
                                          else no!

              - c @
                r count 0
                  inner count 1
                        result = ""
                               @ if cond = 1
                                         @ count
                                    else no!
                tag "If with truthy cond and no do inside a context"

              - c @ wipe

              - c @ put count 1
                        result @ if cond @ count
                                    do foo 2
                                       bar 1

              - c @
                r count 1
                  result = bar 1
                           foo 2
                         @ if cond = 1
                                   @ count
                              do bar 1
                                 foo 2
                tag "If that responds with hash"

              - c @ wipe

              - c @ put count 1
                        result @ if cond @ count
                                    do 2 foo
                                       1 bar

              - c @
                r count 1
                  result = 1 bar
                           2 foo
                         @ if cond = 1
                                   @ count
                              do 1 bar
                                 2 foo
                tag "If that responds with list"

              - c @ wipe

- SEQUENCE - c @ put invalid @ do
                     ref @ invalid 1

           - c @
             r invalid = error "The definition of a sequence must contain a message name and at least one step."
                       @ do
               ref = ""
                   @ invalid 1
             tag "Invoke a sequence that is invalid (no message name, no step)"
             note "We do not get an error because `invalid` points to a hash, so if we get `invalid 1`, we get nothing at all."

           - c @ put invalid @ do - 1

           - c @
             r invalid = error "The definition of a sequence must contain a textual name for its message."
                       @ do 1 1
               ref = error "The definition of a sequence must contain a textual name for its message."
                   @ invalid 1
             tag "Invoke a sequence that is invalid (no message name)"

           - c @ put invalid @ do do - 1

           - c @
             r invalid = error "The name of the message cannot be `do`."
                       @ do do 1 1
               ref = error "The name of the message cannot be `do`."
                   @ invalid 1
             tag "Invoke a sequence that is invalid (invalid message name)"

           - c @ wipe invalid
           - c @ put invalid @ do one - thing
                                  another - thing

           - c @
             r invalid = error "The definition of a sequence can only contain a single name for its message."
                       @ do another - thing
                            one - thing
               ref = error "The definition of a sequence can only contain a single name for its message."
                   @ invalid 1
             tag "Invoke a sequence that is invalid (multiple message names)"


           - c @ wipe invalid

           - c @ put invalid @ do this 2 thing

           - c @
             r invalid = error "The definition of a sequence must start with step number 1."
                       @ do this 2 thing
               ref = error "The definition of a sequence must start with step number 1."
                   @ invalid 1
             tag "Invoke a sequence that is invalid (sequence doesn't start at 1)"

           - c @ wipe invalid

           - c @ put invalid @ do this 1 thing
                                       3 please

           - c @
             r invalid = error "The definition of a sequence cannot have non-consecutive steps."
                       @ do this 1 thing
                                 3 please
               ref = error "The definition of a sequence cannot have non-consecutive steps."
                   @ invalid 1
             tag "Invoke a sequence that is invalid (sequence skips one step)"

           - c @ wipe

           - c @ put multilove @ a thing
                                 something else

           - c @
             r multilove = error "Only one call per prefix is allowed"
                         @ a thing
                           something else
             tag "Only one call allowed per prefix"

           - c @ wipe

           - c @ put eleven @ plus1 10
                     plus1 @ do int - @ add - @ int
                                            - 1
           - c @
             r eleven = 11
                      : int 10
                        do - = 11
                             @ add - = 10
                                     @ int
                                   - 1
                      @ plus1 10
               plus1 = int 1
                     @ do int - @ add - @ int
                                      - 1
             tag "Valid sum: ten plus one"

           - c @ put plus1 @ do int - @ add - @ int
                                            - 2

           - c @
             r eleven = 12
                      : int 10
                        do - = 12
                             @ add - = 10
                                     @ int
                                   - 2
                      @ plus1 10
               plus1 = int 1
                     @ do int - @ add - @ int
                                      - 2
             tag "Update sequence, the sum should update"

           - c @ wipe

           - c @ put eleven @ nested plus1 10
                     nested plus1 @ do int - @ add - @ int
                                                   - 1

           - c @
             r eleven = 11
                      : int 10
                        do 1 = 11
                             @ add - = 10
                                     @ int
                                   - 1
                      @ nested plus1 10
               nested plus1 = int 1
                            @ do int 1 @ add - @ int
                                             - 1
             tag "Call a sequence at a path two steps long"

           - c @ wipe

           - c @ put call @ def bar 1
                                foo 2
                     def @ do message 1 @ message

           - c @
             r call = bar 1
                      foo 2
                    : message bar 1
                              foo 2
                      do 1 = bar 1
                             foo 2
                           @ message
                    @ def bar 1
                          foo 2
               def = message 1
                   @ do message 1 @ message
             tag "Call a sequence with a message that is a hash"

           - c @ wipe

           - c @ put sizzbuzz @ do n - @ put : output ""
                                     - @ if cond @ % - @ n
                                                     - 3
                                            else @ push output sizz
                                     - @ if cond @ % - @ n
                                                     - 5
                                            else @ push output buzz
                                     - @ output
             tag "Define sizzbuzz"

           - c @ put "sizzbuzz 15" @ !izzbuzz 15
             tag "Sizzbuzz 15"

           - c @ wipe
