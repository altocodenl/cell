- "TEXT <> PATHS" - ct ""
                    r ""
                    tag "Empty text"

                  - ct "foo"
                    tag "Single text"

                  - ct "1"
                    tag "Single number"

                  - ct "1"
                    tag "Single number"

                  - ct " "
                    r ""
                    tag "Line with a space"

                  - ct " s"
                    r error "The first line of the message cannot be indented: ` s`."
                    tag "Indented first line"

                  - ct "a	b"
                    r error "The line `a	b` contains a space that should be contained within quotes."
                    tag "Unquoted tab"

                  - ct "holding/"out"
                    r error "The line `holding/"out` has an unescaped quote."
                    tag "Unescaped literal quote"

                  - ct "/"multiline
                         trickery/" some 2 /"calm
                                           animal/""
                    tag "Multiline texts in a long path"

                  - ct "a b
                        c d"
                    tag "Two paths of two steps"

                  - ct "foo bar 1
                           jip 2"
                    r error "The indent of the line `   jip 2` does not match that of the previous line."
                    tag "Indent too short"

                  - ct "widget count 1
                                                jip 2"
                    r error "The indent of the line `                        jip 2` does not match that of the previous line."
                    tag "Indent too long"

                  - ct "widget count 1
                               jip  2"
                    r error "The line `       jip  2` has at least two spaces separating two elements."
                    tag "Too many spaces between paths"

                  - ct "widget count 1
                               price 12"
                    tag "Two paths with one prefix"

                  - ct "widget count 1
                        widget price 12"
                    r "widget count 1
                              price 12"
                    tag "Two unabridged paths with one prefix come back abridged"

                  - ct "deep widget count 1
                                    price 12"
                    tag "Two paths with one prefix two steps long"

                  - ct "deep widget count 1
                        deep widget price 12"

                    r "deep widget count 1
                                   price 12"
                    tag "Two paths with one prefix two steps long come back abridged"

                  - ct "deep widget count 1
                             widget price 12"

                    r "deep widget count 1
                                   price 12"
                    tag "Two paths with one prefix two steps long (semiabridged) come back abridged"

                  - ct "1 foo bar
                        1 jip heh
                        1 sub acu"
                    r "1 foo bar
                         jip heh
                         sub acu"
                    tag "List comes back abridged"

                  - ct "product price 0.1
                                quantity 2.75"
                    tag "Two paths with float values"

                  - ct "product 1 count
                                2 price"
                    tag "List"

                  - ct "something/"foo/" 1"
                    r error "The line `something/"foo/" 1` has an unescaped quote."
                    tag "Unescaped quote in word"

                  - ct "/"foo bar/" 1"
                    tag "Quoted text as path"

                  - ct "/"foo/" /"bar/" 1"
                    r "foo bar 1"
                    tag "Quoted texts that do not need to be quoted come back unquoted"

                  - ct "/"///"foo///" ///"bar///" 1/""
                    tag "Quoted text with quotes inside"

                  - ct "/"foo bar/"x1"
                    r error "No space after a quote in line `/"foo bar/"x1`"
                    tag "No space after quote"

                  - ct "foo /"bar
                             i am on a new line but I am still the same text/" 1"
                    tag "Quote spanning multiple lines"

                  - ct "foo /"1/" bar"
                    tag "Path with text that looks like a number"

                  - ct "foo /"	/" bar"
                    tag "Path with tab"

                  - ct "/"i am text

                         right?
                         /""
                    tag "Quoted text with empty line"

                  - ct "foo /"bar/""
                    r "foo bar"
                    tag "Unnecessary quotes on one step"

                  - ct "foo /"bar yep/""
                    tag "Quoted step in a path"

                  - ct "date 2025-01-01"
                    tag "A date can be a step"

                  - ct "empty /"/" indeed"
                    tag "An empty text can be a step of a path"

                  - ct "/"another multiline

                         /"foo"
                    r error "No space after a quote in line ` /"foo`"
                    tag "No space after multiline text"

                  - ct "/"just multiline
                         ////
                         ///"/""
                    tag "Quoted text with slashes and literal quotes inside"

                  - ct "/"///"/""
                    tag "Single literal quote as step"

                  - ct "/" /////""
                    tag "Space and a slash"

                  - ct "/" /////////""
                    tag "Space and two slashes"

                  - ct "////"
                    tag "Two slashes"

                  - ct "/"/////""
                    r "//"
                    tag "Two slashes unnecessarily quoted"

                  - ct "/"///////"/""
                    tag "Slash and a quote"

                  - ct "/" /////" /" /////////" //// /" ////a/""
                    tag "Space and slash, space and two slashes, space and three slashes, space and four slashes, space, slash and `a`."

                  - ct "history /"1/" from user
                                    message /"@ foo/""
                    tag "Quoted number and quoted text as steps."

                  - ct "history /" /////" from user
                                      message /"@ foo/""
                    tag "Step with multiple slashes"

                  - ct "/" ///""
                    r error "Multiline text not closed: ` /"`"
                    tag "Unclosed multiline"

                  - ct "/" //a/""
                    r error "Unmatched slash in text with spaces or double quotes: ` //a`"
                    tag "Unmatched slash"

                  - ct "/"
                         //a"
                    r error "Unmatched slash in text with spaces or double quotes: `//a`"
                    tag "Unmatched slash in multiline text"

                  - ct "/" //////a"
                    r error "Unmatched slash in text with spaces or double quotes: ` //////a`"
                    tag "Unmatched slash with multiple slashes."

                  - ct "/"A single call must start with ///"@///" but instead starts with ///"w///"/""
                    tag "Long quoted text"

                  - ct "foo - first
                            - second
                            - third"
                    r "foo 1 first
                           2 second
                           3 third"
                    tag "Dedasher with list with three elements"

                  - ct "bar - first
                        foo - second"
                    r "bar 1 first
                       foo 1 second"
                    tag "Dedasher with two consecutive lists"

                  - ct "foo jip 1
                            bar 2"
                    r "foo bar 2
                           jip 1"
                    tag "Sort lowercase hashes"

                  - ct "Foo bar 1
                        foo jip 2"
                    r "foo jip 2
                       Foo bar 1"
                    tag "Uppercase goes after lowercase"

                  - ct "foo bar 1
                        Foo jip 2"
                    r "foo bar 1
                       Foo jip 2"
                    tag "Uppercase goes after lowercase, no-op"

                  - ct "foo bar 1
                            bar 2"
                    r error "The path `foo bar 2` is repeated."
                    tag "Repeated path"

                  - ct ": foo
                        = bar"
                    r "= bar
                       : foo"
                    tag "Standalone colon goes after standalone equal"

                  - ct "=bar 1
                        :foo 2"
                    r ":foo 2
                       =bar 1"
                    tag "Text starting with colon goes before text starting with equal"

                  - ct "foo
                        bar"
                    r error "The path `foo` is repeated."
                    tag "Values without prefixes"

                  - ct "foo 1
                        foo 2"
                    r error "The path `foo 2` is repeated."
                    tag "Repeated hash elements"

                  - ct "foo 1
                        bar 2
                        foo 3"
                    r error "The path `foo 3` is repeated."
                    tag "Repeated non-consecutive hash elements"

                  - ct "foo jip
                            bar 1"
                    r error "The path `foo jip` is setting a text but there is already a hash at path `foo`"
                    tag "Type clash: text on hash"

                  - ct "foo mau5 bar
                                 jip 1"
                    r error "The path `foo mau5 jip 1` is setting a hash but there is already a text at path `foo mau5`"
                    tag "Type clash: hash on text"

                  - ct "foo jip 1
                            2 2"
                    r error "The path `foo jip 1` is setting a hash but there is already a list at path `foo`"
                    tag "Type clash: hash on list"

                  - ct "foo
                        1"
                    r error "The path `foo` is setting a text but there is already a number at path ``"
                    tag "Type clash: text on number"

                  - ct "- yes"
                    r "1 yes"
                    tag "Dashed list with one element"

                  - ct "foo first 1
                        bar x
                        foo second 2"
                    r "bar x
                       foo first 1
                           second 2"
                    tag "Unabridged, unsorted split prefix"

- "JS <> PATHS" - js "{/"c/": /"d/", /"a/": /"b/"}"
                  r "a b
                     c d"
                  tag "Parse JSON object"

                - js "[1, 2, 3]"
                  r "1 1
                     2 2
                     3 3"
                  tag "Parse JSON list"

                - js "/"json/""
                  r "json"
                  tag "Parse JSON text"

                - js "1"
                  r "1"
                  tag "Parse JSON number"

                - js "true"
                  r "1"
                  tag "Parse JSON boolean"

                - js "null"
                  r "/"/""
                  tag "Parse JSON null"

                - js "{/"boo/": [true, false], /"null/": null, /"why/": /"2025-11-12T20:53:20.774Z/"}"
                  r "boo 1 1
                         2 0
                     null /"/"
                     why 2025-11-12T20:53:20.774Z"
                  tag "Parse object with booleans, null and date"

                - cjs ""
                  r "/"/""
                  tag "Empty text outputs empty text"

                - cjs "/"/""
                  r "/"/""
                  tag "An empty text also outputs empty text"

                - cjs "1"
                  r "1"
                  tag "Number to JSON"

                - cjs "/"1/""
                  r "/"1/""
                  tag "Quoted number to JSON"

                - cjs "jup yea
                       soda wey
                       bar 1 jip
                           2 joo"
                  r "{/"bar/": [/"jip/", /"joo/"], /"jup/": /"yea/", /"soda/": /"wey/"}"
                  tag "Four paths into JSON"

- "PUT VALIDATION" - c @ put foo bar 1
                                 jip 2
                     r error "A put call has to be a hash with a value (`v`) and an optional path (`p`)."
                     tag "Invalid put with direct values"

                   - c @ put 1 foo
                             2 bar
                     r error "A put call has to be a hash with a value (`v`) and an optional path (`p`)."
                     tag "Invalid put with numeric keys"

                   - c @ put k foo
                             v bar
                     r error "A put call has to be a hash with a value (`v`) and an optional path (`p`)."
                     tag "Invalid put with wrong key names"

                   - c @ put p foo
                             v bar
                             x jip
                     r error "A put call has to be a hash with a value (`v`) and an optional path (`p`)."
                     tag "Invalid put with extra key x"

                   - c @ put p foo
                             v bar
                             x jip
                     r error "A put call has to be a hash with a value (`v`) and an optional path (`p`)."
                     tag "Invalid put with extra key x (duplicate)"

- "GET & PUT" - c @
                r ""
                tag "Get everything when the dataspace is empty"

              - c @ x
                r ""
                tag "Get one prefix when the dataspace is empty"

              - c @ put p party
                        v list 1 Jan
                               2 Jip
                          open 1
                r ok
                tag "Create a hash with three paths"

              - c @ put p serious
                        v business
                r ok
                tag "Create another hash"

              - c @
                r party list 1 Jan
                             2 Jip
                        open 1
                  serious business
                tag "Get everything and obtain both hashes"

              - c @ party
                r list 1 Jan
                       2 Jip
                  open 1
                tag "Get one prefix to get the entire party"

              - c @ party list
                r 1 Jan
                  2 Jip
                tag "Get a longer prefix to get just the list"

              - c @ party list 1
                r Jan
                tag "Get the first guest only"

              - c @ party list 1 Jan
                r ""
                tag "Get the entire first path and obtain nothing"

              - c @ no such thing
                r ""
                tag "Get no such thing"

              - c @ party open
                r 1
                tag "Get the status of the party"

              - c @ serious
                r business
                tag "Get just the business"

              - c @ serious business
                r ""
                tag "Get the entire last path and obtain nothing"

              - c @ put p party
                        v 1
                tag "Overwrite prefix with multiple paths"

              - c @
                r party 1
                  serious business
                tag "Get everything after overwriting prefix with multiple paths"

              - c @ put p party
                        v list 1 Jan
                               2 Jip
                          open 1
                tag "Restore party"

- WIPE - c @ wipe serious
         r ok
         tag "Wipe serious"

       - c @
         r party list 1 Jan
                      2 Jip
                 open 1
         tag "Get everything after serious was wiped"

       - c @ wipe party list 1 Jan Hertog
         r ok
         tag "Wipe a path that does not exist, should change nothing"

       - c @
         r party list 1 Jan
                      2 Jip
                 open 1
         tag "Get everything after wipe no-op"

       - c @ wipe party list 1
         tag "Wipe the first member of the list"

       - c @ party
         r list 1 Jip
           open 1
         tag "Get party after one element was wiped"

       - c @ wipe
         tag "Wipe everything"

       - c @
         r ""
         tag "Get everything after wipe and obtain nothing"

       - c @ put p nested
                 v 1 list 1 Jan
                          2 Jip
                   2 open
         tag "Setting a nested list"

       - c @ wipe nested 1 list 1
         tag "Wiping part of a nested list"

       - c @
         r nested 1 list 1 Jip
                  2 open
         tag "Get everything after wiping part of a nested list"

       - c @ wipe
         tag "Wipe everything"

- "GET WITH CONTEXT" - c @ put v employee price 15
                                 friend price 20
                                 price 30
                       tag "Initialize the three prices"

                     - c @
                       r employee price 15
                         friend price 20
                         price 30
                       tag "Verify that the three prices are initialized"

                     - c @ price
                       r 30
                       tag "Get outermost price"

                     - c @ put p employee gets
                               v @ price
                       tag "Set reference to employee price"

                     - c @ employee gets
                       r = 15
                         @ price
                       tag "Get employee price"

                     - c @ put p friend gets
                               v @ price
                       tag "Set reference to friend price"

                     - c @ friend gets
                       r = 20
                         @ price
                       tag "Get friend price"

                     - c @ put p outside gets
                               v @ price
                       tag "Set reference to outside price"

                     - c @ outside gets
                       r = 30
                         @ price
                       tag "Get outside price"

                     - c @ wipe

- "GET WITH DOT" - c @ put v employee @ . price
                                      price 15
                             @ . price
                             price 30
                             z @ . price
                   tag "Set two prices and three dot references"

                 - c @
                   r employee price 15
                              = 15
                              @ . price
                     price 30
                     z = ""
                       @ . price
                     = 30
                     @ . price

                   tag "Get all dot references"

                 - c @ wipe

- PUT - c @ put v key value
      - c @
        r dialog 1 c "@ wipe"
                   from user
                   id <OMITTED>
                   ms <OMITTED>
                   r ok
                   to cell
                 2 c "@ put v key value"
                   from user
                   id <OMITTED>
                   ms <OMITTED>
                   r ok
                   to cell
          key value
        keepDialog 1
        tag "Get dataspace including dialog"

      - c @ wipe
      - c @ put p key
                v value
      - c @
        r dialog 1 c "@ wipe"
                   from user
                   id <OMITTED>
                   ms <OMITTED>
                   r ok
                   to cell
                 2 c "@ put p key
                            v value"
                   from user
                   id <OMITTED>
                   ms <OMITTED>
                   r ok
                   to cell
          key value
        keepDialog 1
        tag "Get dataspace including dialog (two path call)"

      - c @ wipe

      - c @ put v data list - a
                            - b

      - c @ put p data
                v just one path

      - c @ data
        r just one path
        tag "Overwrite prefix with multiple paths with one single path"

      - c @ put p data
                v simple

      - c @ data
        r simple
        tag "Overwrite long path with a short one"

      - c @ put p data
                v hash 1

      - c @ put p data 2
                v 3
        r error "The path `data hash 1` is setting a hash but there is already a list at path `data`"
        tag "Attempt to add a list path to a hash"

      - c @ put p data more
                v 3

      - c @ data
        r hash 1
          more 3
        tag "Add another path to a hash prefix"

      - c @ wipe

      - c @ put p foo
                v ""
        r ok

      - c @
        r foo ""
        tag "Empty text is set and retrieved"

      - c @ put p ""
                v bar

      - c @
        r foo ""
          "" bar
        tag "Empty text can be used as path"

      - c @ ""
        r bar
        tag "Get value from path that has an empty text"

      - c @ wipe
      - c @ put v list 1 a
                       2 b

      - c @ put p list foo
                v bar
        r error "The path `list foo bar` is setting a hash but there is already a list at path `list`"
        tag "Attempt to add a hash path to a list"

      - c @ wipe

      - c @ put v hash one thing

      - c @ put p hash "
                              bar"
                v yes
      - c @ hash
        r one thing
          "
                 bar" yes
        tag "Retrieve a value with a path with a step that is multiline text"

      - c @ put p dialog
                v 1
        r error "A dialog cannot be supressed by force."
        tag "Forbid putting to the dialog"

      - c @ put p foo bar jip
                      oh yeah
                v 1
        r error "Only one path can be put at the same time, but received multiple paths: foo bar jip, foo oh yeah"
        tag "Forbid putting multiple paths at the same time"

      - c @ wipe

- "PUT WITH CONTEXT PATH" - c @ put v hook something

                          - c @ put p value
                                    v 1
                            context - foo

                          - c @
                            r hook something
                              value 1
                            tag "Context path does not make a difference if there is no hook"

                          - c @ wipe
                          - c @ put v hook value 0
                          - c @ put p value
                                    v 1

                          - c @
                            r hook value 0
                              value 1
                            tag "Hook without context is irrelevant"

                          - c @ put p value
                                    v 2
                            context - hook

                          - c @
                            r hook value 2
                              value 1
                            tag "Hook with context"

                          - c @ wipe

                          - c @ put v mothership count 1
                                                 deeper context

                          - c @ put p count
                                    v 2
                            context - mothership
                                    - deeper
                                    - context

                          - c @
                            r mothership count 2
                                         deeper context

                          - c @ wipe

- "PUT WITH DOT" - c @ put v ball 1
                             pit ball 2

                 - c @ put p . ball
                           v 3

                 - c @
                   r ball 3
                     pit ball 2
                   tag "Use dot without context"

                 - c @ put p . ball
                           v 4
                   context - completely
                           - different

                 - c @
                   r ball 3
                     completely different ball 4
                     pit ball 2
                   tag "Use dot with non-existing context, creates the context"

                 - c @ put p . ball
                           v 5
                   context - pit

                 - c @
                   r ball 3
                     completely different ball 4
                     pit ball 5
                   tag "Use dot with existing context"

- RESPOND - c @ wipe

          - c @ put v foo 10
                      reffoo @ foo
            r ok
            tag "Set a reference to foo"

          - c @
            r foo 10
              reffoo = 10
                     @ foo
            tag "Get the reference to foo"

          - c @ put p foo
                    v 20
            tag "Update foo"

          - c @
            r foo 20
              reffoo = 20
                     @ foo
            tag "Check that the reference to foo has an updated value"

          - c @ put p reffoo =
                    v 30
            tag "Overwrite the = key of the reference"

          - c @
            r foo 20
              reffoo = 20
                     @ foo
            tag "Check that overwriting the result made no difference"

          - c @ put p a
                    v @ nonesuch

          - c @ a
            r = ""
              @ nonesuch
            tag "Check that a reference to nothing yields an empty text but the reference is still there"

          - c @ wipe

          - c @ put v counter 1
                      indirect @ users @ counter
                      users 1 jip

          - c @
            r counter 1
              indirect = jip
                       @ users = 1
                               @ counter
              users 1 jip
            tag "Use a reference to reference a certain element of a list (two references side by side)"

          - c @ wipe
          - c @ put v aqualung great
                      indirect @ score @ aqualung
                      score great 100

          - c @
            r aqualung great
              indirect = 100
                       @ score = great
                               @ aqualung
              score great 100
            tag "Use a reference to reference a certain element of a hash (two references side by side)"

          - c @ wipe
          - c @ put v closet sock
                      house @ room
                      room @ closet

          - c @
            r closet sock
              house = = sock
                      @ closet
                    @ room
              room = sock
                   @ closet
            tag "Make a reference to a reference in order ba//dc//cb"

          - c @ wipe
          - c @ put v foo bar
                      joo @ jip

          - c @
            r foo bar
              joo = ""
                  @ jip
            tag "Make a reference to a reference in order ba//cb//dc, step one"

          - c @ put v jip @ foo

          - c @
            r foo bar
              jip = bar
                  @ foo
              joo = = bar
                    @ foo
                  @ jip
            tag "Make a reference to a reference in order ba//cb//dc, step two"

          - c @ wipe
          - c @ put v location trompe 1
                      pixies trompe 1 "alec eiffel"
                                    2 mons
                      song @ pixies @ location

          - c @ song
            r = "alec eiffel"
              @ pixies = trompe 1
                       @ location
            tag "Two references side by side, the right one resolving to a one-line hash"

          - c @ wipe
          - c @ put v list 1 place
                           2 without
                           3 a
                           4 postcard
                      ref @ list

          - c @ ref
            r = 1 place
                2 without
                3 a
                4 postcard
              @ list
            tag "Reference a list"

          - c @ put p list
                    v REDACTED

          - c @
            r list REDACTED
              ref = REDACTED
                  @ list
            tag "Overwrite referenced list by text"

          - c @ wipe
          - c @ put v main stores 2
                           total @ widgets
                      widgets 10

          - c @
            r main stores 2
                   total = 10
                         @ widgets
              widgets 10
            tag "Reference inside a hash"

          - c @ wipe
          - c @ put v ned 1 a A C
                          2 b B D
                      quinella 1 a
                               2 b
                      ref @ ned @ quinella

          - c @ ref
            r = A C
              @ ned = 1 a
                      2 b
                    @ quinella
            tag "Getting back two paths as part of a reference, using just the first one"
            note "An alternative way to specify this would be to expect this:

                  = A C
                    B D
                  @ ned = 1 a
                          2 b
                        @ quinella

                  But, for now, we don't need this multireference. It even seems that it's better to just implement multireference as a loop. Time will tell. We go with the above for now.
                  "

- CONDITIONAL

- SEQUENCE - c @ put p call
                     v @ do n - @ if cond 1
                                     do nosuchcall bar
           - c @ put p fizzbuzz
                     v @ do n - @ put p : output
                                        v ""
                              - @ if cond @ % - @ n
                                              - 3
                                     else @ push p output
                                                 v fizz
                              - @ if cond @ % - @ n
                                              - 5
                                     else @ push p output
                                                 v buzz
                              - @ output
             tag "Define fizzbuzz"

           - c @ put p "fizzbuzz 15"
                     v @ !izzbuzz 15
             tag "Fizzbuzz 15"

           - c @ wipe
           - c @ put v eleven @ plus1 10
                       plus1 @ do int - @ + - @ int
                                            - 1
           - c @
             r eleven = 11
                      : int 10
                        seq - = 11
                              @ + - = 10
                                    @ int
                                  - 1
                      @ plus1 10
               plus1 = int 1
                     @ do int - @ + - @ int
                                    - 1
             tag "Valid sum"

           - c @ wipe

